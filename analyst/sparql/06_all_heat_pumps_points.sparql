# All heat pumps: zone temp, discharge air temp, fan status
# Time series: data/heat_pumps.csv (filter by equipment_id, columns: sat, zt, fan_status)

PREFIX brick: <https://brickschema.org/schema/Brick#>
PREFIX ofdd: <http://openfdd.local/ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?equipment_id ?point_type ?csv_column ?brick_class WHERE {
    ?equipment a brick:Heat_Pump .
    BIND(REPLACE(STR(?equipment), ".*#", "") AS ?equipment_id)
    ?point brick:isPointOf ?equipment .
    ?point ofdd:mapsToRuleInput ?rule_input .
    ?point rdfs:label ?csv_column .
    ?point a ?brick_type .
    FILTER(STRSTARTS(STR(?brick_type), STR(brick:)))
    BIND(REPLACE(STR(?brick_type), "https://brickschema.org/schema/Brick#", "") AS ?brick_class)
    BIND(?rule_input AS ?point_type)
}
ORDER BY ?equipment_id ?point_type
